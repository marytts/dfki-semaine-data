group 'de.dfki.mary'
version '0.1-SNAPSHOT'
description 'Speech databases for the SEMAINE TTS voices'

import org.yaml.snakeyaml.*

subprojects {
    ext {
        yamlFile = file("dfki-$project.name-data.yaml")
        flacFile = file("dfki-$project.name-data.flac")
    }

    task processYaml {
        doLast {
            def data = new Yaml().load(yamlFile.text)
            data.each {
                def labFile = new File("$projectDir/lab/${it.prompt}.lab")
                if (labFile.exists()) {
                    def segments = labFile.readLines().drop(1).collect { line ->
                        def fields = line.split('\\s+', 3)
                        [lab: fields[2], end: fields[0] as float]
                    }
                    it << [segments: segments]
                }
            }
            def opts = new DumperOptions()
            opts.defaultFlowStyle = DumperOptions.FlowStyle.BLOCK
            yamlFile.text = new Yaml(opts).dump(data)
        }
    }

    task concatenateAudio(type: Exec) {
        def listFile = file("wav.txt")
        commandLine 'ffmpeg', '-f', 'concat', '-i', listFile, '-acodec', 'flac', '-compression_level', 12, flacFile
        doFirst {
            listFile.withWriter { writer ->
                new Yaml().load(yamlFile.text).each {
                    writer.println "file '$it.file'"
                }
            }
        }
    }

    task extractText(type: ExtractText)

    task extractWav(type: ExtractWav)
}
